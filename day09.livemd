# Day 9: Rope Bridge

```elixir
Mix.install([
  {:kino, "~> 0.7"}
])
```

## Intro

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

## Part 1

This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight.

It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.

You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where **not** to step.

Consider a rope with a knot at each end; these knots mark the **head** and the **tail** of the rope. If the head moves far enough away from the tail, the tail is pulled toward the head.

Due to nebulous reasoning involving [Planck lengths](https://en.wikipedia.org/wiki/Planck_units#Planck_length), you should be able to model the positions of the knots on a two-dimensional grid. Then, by following a hypothetical **series of motions** (your puzzle input) for the head, you can determine how the tail will move.

Due to the aforementioned Planck lengths, the rope must be quite short; in fact, the head (`H`) and tail (`T`) must **always be touching** (diagonally adjacent and even overlapping both count as touching):

```
....
.TH.
....

....
.H..
..T.
....

...
.H. (H covers T)
...
```

If the head is ever two steps directly up, down, left, or right from the tail, the tail must also move one step in that direction so it remains close enough:

```
.....    .....    .....
.TH.. -> .T.H. -> ..TH.
.....    .....    .....

...    ...    ...
.T.    .T.    ...
.H. -> ... -> .T.
...    .H.    .H.
...    ...    ...
```

Otherwise, if the head and tail aren't touching and aren't in the same row or column, the tail always moves one step diagonally to keep up:

```
.....    .....    .....
.....    ..H..    ..H..
..H.. -> ..... -> ..T..
.T...    .T...    .....
.....    .....    .....

.....    .....    .....
.....    .....    .....
..H.. -> ...H. -> ..TH.
.T...    .T...    .....
.....    .....    .....
```

You just need to work out where the tail goes as the head follows a series of motions. Assume the head and the tail both start at the same position, overlapping.

For example:

```
R 4
U 4
L 3
D 1
R 4
D 1
L 5
R 2
```

This series of motions moves the head **right** four steps, then **up** four steps, then **left** three steps, then **down** one step, and so on. After each step, you'll need to update the position of the tail if the step means the head is no longer adjacent to the tail. Visually, these motions occur as follows (`s` marks the starting position as a reference point):

```
== Initial State ==

......
......
......
......
H.....  (H covers T, s)

== R 4 ==

......
......
......
......
TH....  (T covers s)

......
......
......
......
sTH...

......
......
......
......
s.TH..

......
......
......
......
s..TH.

== U 4 ==

......
......
......
....H.
s..T..

......
......
....H.
....T.
s.....

......
....H.
....T.
......
s.....

....H.
....T.
......
......
s.....

== L 3 ==

...H..
....T.
......
......
s.....

..HT..
......
......
......
s.....

.HT...
......
......
......
s.....

== D 1 ==

..T...
.H....
......
......
s.....

== R 4 ==

..T...
..H...
......
......
s.....

..T...
...H..
......
......
s.....

......
...TH.
......
......
s.....

......
....TH
......
......
s.....

== D 1 ==

......
....T.
.....H
......
s.....

== L 5 ==

......
....T.
....H.
......
s.....

......
....T.
...H..
......
s.....

......
......
..HT..
......
s.....

......
......
.HT...
......
s.....

......
......
HT....
......
s.....

== R 2 ==

......
......
.H....  (H covers T)
......
s.....

......
......
.TH...
......
s.....
```

After simulating the rope, you can count up all of the positions the **tail visited at least once**. In this diagram, `s` again marks the starting position (which the tail also visited) and `#` marks other positions the tail visited:

```
..##..
...##.
.####.
....#.
s###..
```

So, there are **13** positions the tail visited at least once.

Simulate your complete hypothetical series of motions. **How many positions does the tail of the rope visit at least once?**

```elixir
defmodule RopeCrawler do
  @doc """
  Calculates positions of head and tail and stores all the positions in the accumulator.

  {0, 0} is the starting position for both head and tail.
  """
  def process(commands) do
    Enum.reduce(commands, {[], {0, 0}, {0, 0}}, &pull/2)
  end

  @doc """
  Do given amount of steps in the given direction and calculates all the position changes
  for both head and tail; puts these positions into accumulator.
  """
  def pull({direction, steps}, {acc, last_h, last_t}) do
    {visited, h_pos, t_pos} =
      Enum.reduce(1..steps, {[], last_h, last_t}, fn _step, {acc, h_pos, t_pos} ->
        h_pos = move_head(h_pos, direction)
        t_pos = move_tail(h_pos, t_pos, direction)

        {[t_pos | acc], h_pos, t_pos}
      end)

    {[visited | acc], h_pos, t_pos}
  end

  # for head position nothing fancy – just move it where it says – one step at a time
  defp move_head({h_x, h_y}, "R"), do: {h_x + 1, h_y}
  defp move_head({h_x, h_y}, "L"), do: {h_x - 1, h_y}
  defp move_head({h_x, h_y}, "U"), do: {h_x, h_y + 1}
  defp move_head({h_x, h_y}, "D"), do: {h_x, h_y - 1}

  # no need to move tail when it's very close to the head
  defp move_tail({h_x, h_y}, {t_x, t_y}, _direction)
       when abs(h_x - t_x) <= 1 and abs(h_y - t_y) <= 1,
       do: {t_x, t_y}

  # head and tail are in the same row
  defp move_tail({_, y}, {t_x, y}, "R"), do: {t_x + 1, y}
  defp move_tail({_, y}, {t_x, y}, "L"), do: {t_x - 1, y}
  defp move_tail({_, y}, {t_x, y}, "U"), do: {t_x, y}
  defp move_tail({_, y}, {t_x, y}, "D"), do: {t_x, y}

  # head and tail are in the same column
  defp move_tail({x, _}, {x, t_y}, "R"), do: {x, t_y}
  defp move_tail({x, _}, {x, t_y}, "L"), do: {x, t_y}
  defp move_tail({x, _}, {x, t_y}, "U"), do: {x, t_y + 1}
  defp move_tail({x, _}, {x, t_y}, "D"), do: {x, t_y - 1}

  # head and tail are in different columns/rows
  defp move_tail({_, h_y}, {t_x, _}, "R"), do: {t_x + 1, h_y}
  defp move_tail({_, h_y}, {t_x, _}, "L"), do: {t_x - 1, h_y}
  defp move_tail({h_x, _}, {_, t_y}, "U"), do: {h_x, t_y + 1}
  defp move_tail({h_x, _}, {_, t_y}, "D"), do: {h_x, t_y - 1}
end

input
|> Kino.Input.read()
|> String.split("\n")
|> Enum.map(fn cmd ->
  [direction, steps] = String.split(cmd, " ")
  {direction, String.to_integer(steps)}
end)
|> RopeCrawler.process()
|> elem(0)
|> List.flatten()
|> Enum.uniq()
|> length()
```
