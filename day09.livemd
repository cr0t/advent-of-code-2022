# Day 9: Rope Bridge

```elixir
Mix.install([
  {:kino, "~> 0.7"}
])
```

## Intro

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

## Part 1

This rope bridge creaks as you walk along it. You aren't sure how old it is, or whether it can even support your weight.

It seems to support the Elves just fine, though. The bridge spans a gorge which was carved out by the massive river far below you.

You step carefully; as you do, the ropes stretch and twist. You decide to distract yourself by modeling rope physics; maybe you can even figure out where **not** to step.

Consider a rope with a knot at each end; these knots mark the **head** and the **tail** of the rope. If the head moves far enough away from the tail, the tail is pulled toward the head.

Due to nebulous reasoning involving [Planck lengths](https://en.wikipedia.org/wiki/Planck_units#Planck_length), you should be able to model the positions of the knots on a two-dimensional grid. Then, by following a hypothetical **series of motions** (your puzzle input) for the head, you can determine how the tail will move.

Due to the aforementioned Planck lengths, the rope must be quite short; in fact, the head (`H`) and tail (`T`) must **always be touching** (diagonally adjacent and even overlapping both count as touching):

```
....
.TH.
....

....
.H..
..T.
....

...
.H. (H covers T)
...
```

If the head is ever two steps directly up, down, left, or right from the tail, the tail must also move one step in that direction so it remains close enough:

```
.....    .....    .....
.TH.. -> .T.H. -> ..TH.
.....    .....    .....

...    ...    ...
.T.    .T.    ...
.H. -> ... -> .T.
...    .H.    .H.
...    ...    ...
```

Otherwise, if the head and tail aren't touching and aren't in the same row or column, the tail always moves one step diagonally to keep up:

```
.....    .....    .....
.....    ..H..    ..H..
..H.. -> ..... -> ..T..
.T...    .T...    .....
.....    .....    .....

.....    .....    .....
.....    .....    .....
..H.. -> ...H. -> ..TH.
.T...    .T...    .....
.....    .....    .....
```

You just need to work out where the tail goes as the head follows a series of motions. Assume the head and the tail both start at the same position, overlapping.

For example:

```
R 4
U 4
L 3
D 1
R 4
D 1
L 5
R 2
```

This series of motions moves the head **right** four steps, then **up** four steps, then **left** three steps, then **down** one step, and so on. After each step, you'll need to update the position of the tail if the step means the head is no longer adjacent to the tail. Visually, these motions occur as follows (`s` marks the starting position as a reference point):

```
== Initial State ==

......
......
......
......
H.....  (H covers T, s)

== R 4 ==

......
......
......
......
TH....  (T covers s)

......
......
......
......
sTH...

......
......
......
......
s.TH..

......
......
......
......
s..TH.

== U 4 ==

......
......
......
....H.
s..T..

......
......
....H.
....T.
s.....

......
....H.
....T.
......
s.....

....H.
....T.
......
......
s.....

== L 3 ==

...H..
....T.
......
......
s.....

..HT..
......
......
......
s.....

.HT...
......
......
......
s.....

== D 1 ==

..T...
.H....
......
......
s.....

== R 4 ==

..T...
..H...
......
......
s.....

..T...
...H..
......
......
s.....

......
...TH.
......
......
s.....

......
....TH
......
......
s.....

== D 1 ==

......
....T.
.....H
......
s.....

== L 5 ==

......
....T.
....H.
......
s.....

......
....T.
...H..
......
s.....

......
......
..HT..
......
s.....

......
......
.HT...
......
s.....

......
......
HT....
......
s.....

== R 2 ==

......
......
.H....  (H covers T)
......
s.....

......
......
.TH...
......
s.....
```

After simulating the rope, you can count up all of the positions the **tail visited at least once**. In this diagram, `s` again marks the starting position (which the tail also visited) and `#` marks other positions the tail visited:

```
..##..
...##.
.####.
....#.
s###..
```

So, there are **13** positions the tail visited at least once.

Simulate your complete hypothetical series of motions. **How many positions does the tail of the rope visit at least once?**

```elixir
defmodule KnotMover do
  @doc """
  For heading knot position nothing fancy – just move it where it says – one step at a time
  """
  def move_head({h_x, h_y}, "R"), do: {h_x + 1, h_y}
  def move_head({h_x, h_y}, "L"), do: {h_x - 1, h_y}
  def move_head({h_x, h_y}, "U"), do: {h_x, h_y + 1}
  def move_head({h_x, h_y}, "D"), do: {h_x, h_y - 1}

  @doc """
  Calculates position of a tailing knot, based on its heading one (it must follow it).
  """
  def move_tail(h_pos, t_pos)

  # no need to move tail when it's very close to the head
  def move_tail({h_x, h_y}, {t_x, t_y}) when abs(h_x - t_x) <= 1 and abs(h_y - t_y) <= 1,
    do: {t_x, t_y}

  # head and tail are in the same row
  def move_tail({h_x, y}, {t_x, y}) when t_x - h_x > 1, do: {t_x - 1, y}
  def move_tail({h_x, y}, {t_x, y}) when t_x - h_x < -1, do: {t_x + 1, y}

  # head and tail are in the same column
  def move_tail({x, h_y}, {x, t_y}) when t_y - h_y > 1, do: {x, t_y - 1}
  def move_tail({x, h_y}, {x, t_y}) when t_y - h_y < -1, do: {x, t_y + 1}

  # if the head and tail aren't touching and aren't in the same row or column,
  # the tail always moves one step diagonally to keep up
  def move_tail({h_x, h_y}, {t_x, t_y}) do
    cond do
      # upper right
      h_y > t_y and h_x > t_x -> {t_x + 1, t_y + 1}
      # upper left
      h_y > t_y -> {t_x - 1, t_y + 1}
      # lower right
      h_y < t_y and h_x > t_x -> {t_x + 1, t_y - 1}
      # lower left
      h_y < t_y -> {t_x - 1, t_y - 1}
    end
  end
end

defmodule RopeCrawler do
  @doc """
  Calculates positions of head and tail and stores all the positions in the accumulator.

  {0, 0} is the starting position for both head and tail.
  """
  def process(commands) do
    Enum.reduce(commands, {[], {0, 0}, {0, 0}}, &pull/2)
  end

  @doc """
  Do given amount of steps in the given direction and calculates all the position changes
  for both head and tail; puts these positions into accumulator.
  """
  def pull({direction, steps}, {acc, last_h, last_t}) do
    {visited, h_pos, t_pos} =
      Enum.reduce(1..steps, {[], last_h, last_t}, fn _step, {acc, h_pos, t_pos} ->
        h_pos = KnotMover.move_head(h_pos, direction)
        t_pos = KnotMover.move_tail(h_pos, t_pos)

        {[t_pos | acc], h_pos, t_pos}
      end)

    {[visited | acc], h_pos, t_pos}
  end
end

input
|> Kino.Input.read()
|> String.split("\n")
|> Enum.map(fn cmd ->
  [direction, steps] = String.split(cmd, " ")
  {direction, String.to_integer(steps)}
end)
|> RopeCrawler.process()
|> elem(0)
|> List.flatten()
|> Enum.uniq()
|> length()
```

## Part 2

A rope snaps! Suddenly, the river is getting a lot closer than you remember. The bridge is still there, but some of the ropes that broke are now whipping toward you as you fall through the air!

The ropes are moving too quickly to grab; you only have a few seconds to choose how to arch your body to avoid being hit. Fortunately, your simulation can be extended to support longer ropes.

Rather than two knots, you now must simulate a rope consisting of **ten** knots. One knot is still the head of the rope and moves according to the series of motions. Each knot further down the rope follows the knot in front of it using the same rules as before.

Using the same series of motions as the above example, but with the knots marked `H`, `1`, `2`, ..., `9`, the motions now occur as follows:

```
== Initial State ==

......
......
......
......
H.....  (H covers 1, 2, 3, 4, 5, 6, 7, 8, 9, s)

== R 4 ==

......
......
......
......
1H....  (1 covers 2, 3, 4, 5, 6, 7, 8, 9, s)

......
......
......
......
21H...  (2 covers 3, 4, 5, 6, 7, 8, 9, s)

......
......
......
......
321H..  (3 covers 4, 5, 6, 7, 8, 9, s)

......
......
......
......
4321H.  (4 covers 5, 6, 7, 8, 9, s)

== U 4 ==

......
......
......
....H.
4321..  (4 covers 5, 6, 7, 8, 9, s)

......
......
....H.
.4321.
5.....  (5 covers 6, 7, 8, 9, s)

......
....H.
....1.
.432..
5.....  (5 covers 6, 7, 8, 9, s)

....H.
....1.
..432.
.5....
6.....  (6 covers 7, 8, 9, s)

== L 3 ==

...H..
....1.
..432.
.5....
6.....  (6 covers 7, 8, 9, s)

..H1..
...2..
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

.H1...
...2..
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

== D 1 ==

..1...
.H.2..
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

== R 4 ==

..1...
..H2..
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

..1...
...H..  (H covers 2)
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

......
...1H.  (1 covers 2)
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

......
...21H
..43..
.5....
6.....  (6 covers 7, 8, 9, s)

== D 1 ==

......
...21.
..43.H
.5....
6.....  (6 covers 7, 8, 9, s)

== L 5 ==

......
...21.
..43H.
.5....
6.....  (6 covers 7, 8, 9, s)

......
...21.
..4H..  (H covers 3)
.5....
6.....  (6 covers 7, 8, 9, s)

......
...2..
..H1..  (H covers 4; 1 covers 3)
.5....
6.....  (6 covers 7, 8, 9, s)

......
...2..
.H13..  (1 covers 4)
.5....
6.....  (6 covers 7, 8, 9, s)

......
......
H123..  (2 covers 4)
.5....
6.....  (6 covers 7, 8, 9, s)

== R 2 ==

......
......
.H23..  (H covers 1; 2 covers 4)
.5....
6.....  (6 covers 7, 8, 9, s)

......
......
.1H3..  (H covers 2, 4)
.5....
6.....  (6 covers 7, 8, 9, s)
```

Now, you need to keep track of the positions the new tail, `9`, visits. In this example, the tail never moves, and so it only visits **1** position. However, **be careful**: more types of motion are possible than before, so you might want to visually compare your simulated rope to the one above.

Here's a larger example:

```
R 5
U 8
L 8
D 3
R 17
D 10
L 25
U 20
```

These motions occur as follows (individual steps are not shown):

```
== Initial State ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
...........H..............  (H covers 1, 2, 3, 4, 5, 6, 7, 8, 9, s)
..........................
..........................
..........................
..........................
..........................

== R 5 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
...........54321H.........  (5 covers 6, 7, 8, 9, s)
..........................
..........................
..........................
..........................
..........................

== U 8 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
................H.........
................1.........
................2.........
................3.........
...............54.........
..............6...........
.............7............
............8.............
...........9..............  (9 covers s)
..........................
..........................
..........................
..........................
..........................

== L 8 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
........H1234.............
............5.............
............6.............
............7.............
............8.............
............9.............
..........................
..........................
...........s..............
..........................
..........................
..........................
..........................
..........................

== D 3 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
.........2345.............
........1...6.............
........H...7.............
............8.............
............9.............
..........................
..........................
...........s..............
..........................
..........................
..........................
..........................
..........................

== R 17 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
................987654321H
..........................
..........................
..........................
..........................
...........s..............
..........................
..........................
..........................
..........................
..........................

== D 10 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
...........s.........98765
.........................4
.........................3
.........................2
.........................1
.........................H

== L 25 ==

..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
...........s..............
..........................
..........................
..........................
..........................
H123456789................

== U 20 ==

H.........................
1.........................
2.........................
3.........................
4.........................
5.........................
6.........................
7.........................
8.........................
9.........................
..........................
..........................
..........................
..........................
..........................
...........s..............
..........................
..........................
..........................
..........................
..........................
```

Now, the tail (`9`) visits **36** positions (including `s`) at least once:

```
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
..........................
#.........................
#.............###.........
#............#...#........
.#..........#.....#.......
..#..........#.....#......
...#........#.......#.....
....#......s.........#....
.....#..............#.....
......#............#......
.......#..........#.......
........#........#........
.........########.........
```

Simulate your complete series of motions on a larger rope with ten knots. **How many positions does the tail of the rope visit at least once?**

```elixir
defmodule LongRopeCrawler do
  @knots %{
    0 => {0, 0},
    1 => {0, 0},
    2 => {0, 0},
    3 => {0, 0},
    4 => {0, 0},
    5 => {0, 0},
    6 => {0, 0},
    7 => {0, 0},
    8 => {0, 0},
    9 => {0, 0}
  }

  @doc """
  Calculates positions of head and tail and stores all the positions in the accumulator.

  {0, 0} is the starting position for all knots in the rope.
  """
  def process(commands) do
    Enum.reduce(commands, {[], @knots}, &pull/2)
  end

  @doc """
  Do given amount of steps in the given direction and calculates all the position changes
  for all knots in the rope; puts these positions into accumulator.
  """
  def pull({direction, steps}, {acc, knots}) do
    {acc, knots} =
      Enum.reduce(1..steps, {acc, knots}, fn _step, {acc, knots} ->
        knots =
          Enum.reduce(0..9, knots, fn
            0, knots -> Map.put(knots, 0, KnotMover.move_head(knots[0], direction))
            i, knots -> Map.put(knots, i, KnotMover.move_tail(knots[i - 1], knots[i]))
          end)

        # debug(knots, direction)

        {[knots[9] | acc], knots}
      end)

    {acc, knots}
  end

  defp debug(knots, direction) do
    field =
      Enum.map(1..6, fn _ ->
        List.duplicate(nil, 6)
        |> List.to_tuple()
      end)
      |> List.to_tuple()

    {field, _} =
      Enum.reduce(9..0, {field, knots}, fn i, {field, knots} ->
        {x, y} = knots[i]

        row =
          elem(field, y)
          |> put_elem(x, i)

        {put_elem(field, y, row), knots}
      end)

    IO.puts("\ndirection #{direction}")

    field
    |> Tuple.to_list()
    |> Enum.map(fn row ->
      row
      |> Tuple.to_list()
      |> Enum.map(fn
        nil -> "."
        knot -> knot
      end)
      |> Enum.join()
    end)
    |> Enum.reverse()
    |> Enum.join("\n")
    |> IO.puts()
  end
end

input
|> Kino.Input.read()
|> String.split("\n")
|> Enum.map(fn cmd ->
  [direction, steps] = String.split(cmd, " ")
  {direction, String.to_integer(steps)}
end)
|> LongRopeCrawler.process()
|> elem(0)
|> Enum.uniq()
|> length()
```
