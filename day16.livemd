# Day 16: Proboscidea Volcanium

```elixir
Mix.install([{:kino, "~> 0.8.0"}, {:libgraph, "~> 0.16.0"}])
```

## Input

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

```elixir
defmodule Day16Shared do
  def parse(input) do
    input
    |> Kino.Input.read()
    |> String.split("\n")
    |> Enum.reduce({%{}, Graph.new()}, fn raw, {rates, graph} ->
      # basic parsing
      regex = ~r/Valve ([A-Z]{2}) .+ rate=(\d+); .+ valves? ([A-Z, ]+$)/
      [valve, rate, leads] = Regex.run(regex, raw, capture: :all_but_first)
      [valve, rate, leads] = [valve, String.to_integer(rate), String.split(leads, ", ")]

      # build edges
      edges = Enum.map(leads, &{valve, &1, weight: 1})

      # put data into containers
      graph = Graph.add_vertex(graph, valve) |> Graph.add_edges(edges)
      rates = Map.put(rates, valve, rate)

      {rates, graph}
    end)
  end

  def render(graph) do
    edges =
      for %{v1: v1, v2: v2} <- Graph.edges(graph) do
        hops = Graph.get_shortest_path(graph, v1, v2) |> Enum.count()
        "#{v1}<-->|#{hops}|#{v2};"
      end

    ["graph LR;" | edges]
    |> Enum.join("\n")
    |> Kino.Mermaid.new()
  end
end

{_rates, graph} = Day16Shared.parse(input)
Day16Shared.render(graph)
```

## Part 1

The sensors have led you to the origin of the distress signal: yet another handheld device, just like the one the Elves gave you. However, you don't see any Elves around; instead, the device is surrounded by elephants! They must have gotten lost in these tunnels, and one of the elephants apparently figured out how to turn on the distress signal.

The ground rumbles again, much stronger this time. What kind of cave is this, exactly? You scan the cave with your handheld device; it reports mostly igneous rock, some ash, pockets of pressurized gas, magma... this isn't just a cave, it's a volcano!

You need to get the elephants out of here, quickly. Your device estimates that you have **30 minutes** before the volcano erupts, so you don't have time to go back out the way you came in.

You scan the cave for other options and discover a network of pipes and pressure-release **valves**. You aren't sure how such a system got into a volcano, but you don't have time to complain; your device produces a report (your puzzle input) of each valve's **flow rate** if it were opened (in pressure per minute) and the tunnels you could use to move between the valves.

There's even a valve in the room you and the elephants are currently standing in labeled `AA`. You estimate it will take you one minute to open a single valve and one minute to follow any tunnel from one valve to another. What is the most pressure you could release?

For example, suppose you had the following scan output:

```
Valve AA has flow rate=0; tunnels lead to valves DD, II, BB
Valve BB has flow rate=13; tunnels lead to valves CC, AA
Valve CC has flow rate=2; tunnels lead to valves DD, BB
Valve DD has flow rate=20; tunnels lead to valves CC, AA, EE
Valve EE has flow rate=3; tunnels lead to valves FF, DD
Valve FF has flow rate=0; tunnels lead to valves EE, GG
Valve GG has flow rate=0; tunnels lead to valves FF, HH
Valve HH has flow rate=22; tunnel leads to valve GG
Valve II has flow rate=0; tunnels lead to valves AA, JJ
Valve JJ has flow rate=21; tunnel leads to valve II
```

All of the valves begin **closed**. You start at valve `AA`, but it must be damaged or jammed or something: its flow rate is `0`, so there's no point in opening it. However, you could spend one minute moving to valve `BB` and another minute opening it; doing so would release pressure during the remaining **28 minutes** at a flow rate of `13`, a total eventual pressure release of `28 * 13 = 364`. Then, you could spend your third minute moving to valve `CC` and your fourth minute opening it, providing an additional **26 minutes** of eventual pressure release at a flow rate of `2`, or **52** total pressure released by valve `CC`.

Making your way through the tunnels like this, you could probably open many or all of the valves by the time 30 minutes have elapsed. However, you need to release as much pressure as possible, so you'll need to be methodical. Instead, consider this approach:

```
== Minute 1 ==
No valves are open.
You move to valve DD.

== Minute 2 ==
No valves are open.
You open valve DD.

== Minute 3 ==
Valve DD is open, releasing 20 pressure.
You move to valve CC.

== Minute 4 ==
Valve DD is open, releasing 20 pressure.
You move to valve BB.

== Minute 5 ==
Valve DD is open, releasing 20 pressure.
You open valve BB.

== Minute 6 ==
Valves BB and DD are open, releasing 33 pressure.
You move to valve AA.

== Minute 7 ==
Valves BB and DD are open, releasing 33 pressure.
You move to valve II.

== Minute 8 ==
Valves BB and DD are open, releasing 33 pressure.
You move to valve JJ.

== Minute 9 ==
Valves BB and DD are open, releasing 33 pressure.
You open valve JJ.

== Minute 10 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve II.

== Minute 11 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve AA.

== Minute 12 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve DD.

== Minute 13 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve EE.

== Minute 14 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve FF.

== Minute 15 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve GG.

== Minute 16 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve HH.

== Minute 17 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You open valve HH.

== Minute 18 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve GG.

== Minute 19 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve FF.

== Minute 20 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve EE.

== Minute 21 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You open valve EE.

== Minute 22 ==
Valves BB, DD, EE, HH, and JJ are open, releasing 79 pressure.
You move to valve DD.

== Minute 23 ==
Valves BB, DD, EE, HH, and JJ are open, releasing 79 pressure.
You move to valve CC.

== Minute 24 ==
Valves BB, DD, EE, HH, and JJ are open, releasing 79 pressure.
You open valve CC.

== Minute 25 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 26 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 27 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 28 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 29 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 30 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.
```

This approach lets you release the most pressure possible in 30 minutes with this valve layout, **1651**.

Work out the steps to release the most pressure in 30 minutes. **What is the most pressure you can release?**

```elixir
defmodule Day16Part1 do
  @limit 30
  @start "AA"

  def solve({rates, graph}) do
    to_be_opened = worth_to_open(rates)
    costs = travel_costs(graph, to_be_opened)

    flow(@start, to_be_opened, costs, @limit, [])
  end

  @doc """
  Filters out only valves with non-zero flow rate.

  P.S. Adds "AA" just to have a starting point there.
  """
  def worth_to_open(rates) do
    rates
    |> Enum.filter(fn {_valve, rate} -> rate > 0 end)
    |> Enum.into(%{})
    |> Map.put(@start, 0)
  end

  @doc """
  Find distances between all the given valves.

  To minify size of the resulting container we order valves names; so we won't
  have double in the result. For example, without this sorting we would have this:

  %{
    # ...
    {"AA", "CC"} => 3,
    {"CC", "AA"} => 3,
    # ...
  }

  With simple `a < b` guard we will have only {"AA", "CC"} => 3 record.

  See `cost/3` clauses to get how we use this trick later.
  """
  def travel_costs(graph, worthy) do
    for {a, _} <- worthy, {b, _} <- worthy, a != b, a < b, into: %{} do
      hops = Graph.get_shortest_path(graph, a, b) |> Enum.count()

      {{a, b}, hops}
    end
  end

  @doc """
  Dynamically finds the best released pressure we can get from visiting all the
  valves worth to open.

  It takes into consideration travel costs that is needed to move to the next
  valve, and minutes remaining.

  When time is over or when we do not have valves to open, we calculate the final
  pressure released within the path (that we store in the opened list argument).
  """
  def flow(valve, valves, costs, minutes, opened)

  def flow(_, valves, _, minutes, opened) when map_size(valves) == 0 or minutes <= 0 do
    for {_, rate, opened_at} <- opened, reduce: 0 do
      acc -> acc + opened_at * rate
    end
  end

  def flow(valve, valves, costs, minutes, opened) do
    for {next, rate} <- valves, next != valve, reduce: 0 do
      pressure ->
        travel = cost(costs, valve, next)
        remain = minutes - travel

        max(
          pressure,
          flow(next, Map.delete(valves, next), costs, remain, [{next, rate, remain} | opened])
        )
    end
  end

  @doc """
  Returns the travel distance from node A to B.

  P.S. We need the first (guarded) clause here to find a correct key, because
  we sorted keys of the costs to minimize number of them (see travel_costs/2).
  """
  def cost(costs, a, b) when b < a, do: cost(costs, b, a)
  def cost(costs, a, b), do: Map.fetch!(costs, {a, b})
end

# 1595 is right answer
input
|> Day16Shared.parse()
|> Day16Part1.solve()
```

## Part 2

You're worried that even with an optimal approach, the pressure released won't be enough. What if you got one of the elephants to help you?

It would take you 4 minutes to teach an elephant how to open the right valves in the right order, leaving you with only **26 minutes** to actually execute your plan. Would having two of you working together be better, even if it means having less time? (Assume that you teach the elephant before opening any valves yourself, giving you both the same full 26 minutes.)

In the example above, you could teach the elephant to help you as follows:

```
== Minute 1 ==
No valves are open.
You move to valve II.
The elephant moves to valve DD.

== Minute 2 ==
No valves are open.
You move to valve JJ.
The elephant opens valve DD.

== Minute 3 ==
Valve DD is open, releasing 20 pressure.
You open valve JJ.
The elephant moves to valve EE.

== Minute 4 ==
Valves DD and JJ are open, releasing 41 pressure.
You move to valve II.
The elephant moves to valve FF.

== Minute 5 ==
Valves DD and JJ are open, releasing 41 pressure.
You move to valve AA.
The elephant moves to valve GG.

== Minute 6 ==
Valves DD and JJ are open, releasing 41 pressure.
You move to valve BB.
The elephant moves to valve HH.

== Minute 7 ==
Valves DD and JJ are open, releasing 41 pressure.
You open valve BB.
The elephant opens valve HH.

== Minute 8 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve CC.
The elephant moves to valve GG.

== Minute 9 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You open valve CC.
The elephant moves to valve FF.

== Minute 10 ==
Valves BB, CC, DD, HH, and JJ are open, releasing 78 pressure.
The elephant moves to valve EE.

== Minute 11 ==
Valves BB, CC, DD, HH, and JJ are open, releasing 78 pressure.
The elephant opens valve EE.

(At this point, all valves are open.)

== Minute 12 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

...

== Minute 20 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

...

== Minute 26 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.
```

With the elephant helping, after 26 minutes, the best you could do would release a total of **1707** pressure.

**With you and an elephant working together for 26 minutes, what is the most pressure you could release?**

<!-- livebook:{"break_markdown":true} -->

### DISCLAIMER

I was unable to solve the second part by myself and looked for hints and others' solutions. _To be fair, this task was hard for lots of folks._

I found [a few solutions](https://elixirforum.com/t/advent-of-code-2022-day-16/52585/5) on the Elixir Forum, then took one and tried to comprehend it. Original solution is [here](https://github.com/stevensonmt/advent_of_code/blob/2022/2022/day16/lib/day16.ex); it is also a "copy" of someone's Rust code.

I slightly re-wrote it _(as I don't store flow rates in the graph, and store distances in half)_, refactored a bit _(mostly just changed variables' and methods' names)_, and tried to comprehend by reading original code and comments and writing my own comments.

I also learned about [`:array` Erlang's module](https://www.erlang.org/doc/man/array.html).

```elixir
defmodule Day16Part2 do
  import Bitwise, only: [&&&: 2, "~~~": 1]
  import Day16Part1, only: [worth_to_open: 1, travel_costs: 2, cost: 3]

  @limit 26
  @start "AA"

  def solve({rates, graph}) do
    worthy = worth_to_open(rates)
    distances = travel_costs(graph, worthy)
    to_be_opened = Map.delete(worthy, "AA") |> Map.keys() |> :array.from_list()

    elephant_in_the_room(distances, to_be_opened, rates)
  end

  @doc """
  Finds the best path for each subset of targets and match with the best complementary
  subset; then take the best value combination.

  P.S. The name of this function just shows that it's not clear HOW EXACTLY it works :)
  """
  def elephant_in_the_room(distances, to_be_opened, rates) do
    valves_n = :array.sparse_size(to_be_opened)
    bits_n = Integer.pow(2, valves_n) - 1

    pressures = pressures(distances, to_be_opened, rates)

    for me <- 1..bits_n,
        el = bitstring_complement(me, valves_n),
        sum = Enum.reduce([me, el], 0, fn i, sum -> sum + Map.fetch!(pressures, i) end),
        reduce: 0 do
      best -> max(best, sum)
    end
  end

  @doc """
  Finds the highest value path for each target valve.
  """
  def pressures(distances, to_be_opened, rates) do
    valves_n = :array.sparse_size(to_be_opened)
    bits_n = Integer.pow(2, valves_n) - 1

    1..bits_n
    |> Enum.map(&{&1, targets_for_bit(&1, valves_n, to_be_opened)})
    |> Enum.map(fn {me, targets} ->
      {me, best_path(distances, targets, rates, @start, @limit)}
    end)
    |> Enum.into(%{})
    |> Map.put(0, 0)
  end

  @doc """
  Digits of the index become flags for indices of the elements in the current
  subset of targets. For example, idx = 5 gives bits of 101, so take targets[0]
  and targets[2].

  The bits are masked by the upper limit of possible indices to avoid
  overflow/underflow errors.
  """
  def targets_for_bit(idx, count, to_be_opened) do
    for i <- 0..(count - 1), anded = idx &&& Integer.pow(2, i), anded > 0 do
      :array.get(i, to_be_opened)
    end
  end

  @doc """
  Get the indices for the complementary subset to the current subset defined by idx.

  For example, idx = 5 gives bits of 101.

  If there are 6 valves with flow > 0 (as in the example), then you have 2^6 - 1 maximum
  indices. Use that max to mask the flipped bits to avoid overflow/underflow issues.

  P.S. Bitwise not (~~~ operator) flips the bits.
  """
  def bitstring_complement(idx, count) do
    mask = Integer.pow(2, count) - 1
    flipped = ~~~idx

    Bitwise.band(flipped, mask)
  end

  @doc """
  For each (to be opened) target, find the path from the given start node to that
  target that leads to the optimal complete path (i.e., the path resulting in either
  running out of time or opening all valves).
  """
  def best_path(distances, to_be_opened, rates, start, time_left) do
    for target <- to_be_opened,
        travel = cost(distances, start, target),
        travel < time_left,
        relief_duration = time_left - travel,
        pressure = rates[target] * relief_duration,
        to_visit = Enum.reject(to_be_opened, &(&1 == target)),
        reduce: 0 do
      best ->
        if to_visit == [] do
          max(best, pressure)
        else
          max(best, pressure + best_path(distances, to_visit, rates, target, relief_duration))
        end
    end
  end
end

# 2178 is too low
# 2189 is the right answer
input
|> Day16Shared.parse()
|> Day16Part2.solve()
```
