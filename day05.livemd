# Day 5: Supply Stacks

```elixir
Mix.install([
  {:kino, "~> 0.7"}
])
```

## Intro

```elixir
input = Kino.Input.textarea("Please paste your input:")
```

## Part 1

The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked **crates**, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a **giant cargo crane** capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her **which** crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates **and** the rearrangement procedure (your puzzle input). For example:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
```

In this example, there are three stacks of crates. Stack 1 contains two crates: crate `Z` is on the bottom, and crate `N` is on top. Stack 2 contains three crates; from bottom to top, they are crates `M`, `C`, and `D`. Finally, stack 3 contains a single crate, `P`.

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

In the second step, three crates are moved from stack 1 to stack 3. Crates are moved **one at a time**, so the first crate to be moved (`D`) ends up below the second and third crates:

```
        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
```

Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved **one at a time**, crate `C` ends up below crate `M`:

```
        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3
```

Finally, one crate is moved from stack 1 to stack 2:

```
        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3
```

The Elves just need to know **which crate will end up on top of each stack**; in this example, the top crates are `C` in stack 1, `M` in stack 2, and `Z` in stack 3, so you should combine these together and give the Elves the message **`CMZ`**.

**After the rearrangement procedure completes, what crate ends up on top of each stack?**

````elixir
[starting_stacks, procedure] =
  input
  |> Kino.Input.read()
  |> String.split("\n\n")

defmodule Stacker do
  @moduledoc false

  @doc """
  Converts the "drawing" input of the stacks state into a Map with keys as
  stacks' names, and Lists as contents of each stack.

  So, for such an input:

  ```
      [D]    
  [N] [C]    
  [Z] [M] [P]
   1   2   3 
  ```

  Will be converted to:

  %{
    "1" => ["N", "Z"],
    "2" => ["D", "C", "M"],
    "3" => ["P"]
  }
  """
  def parse(starting_stacks_input) do
    starting_stacks_input
    |> String.split("\n")
    |> Enum.map(fn line ->
      line
      |> String.codepoints()
      # we are interested only in the crates' names and ignore whitespaces between
      |> Enum.chunk_every(3, 4)
      |> Enum.map(fn [_, crate, _] -> crate end)
    end)
    |> Enum.reverse()
    # transpose to get this structure: [["1", "Z", "N", " "], ["2", "M", "C", "D"], ...]
    |> Enum.zip_with(& &1)
    |> Enum.reduce(%{}, fn [stack | crates], stacks ->
      crates =
        crates
        |> Enum.reject(&(&1 == " "))
        |> Enum.reverse()

      Map.put(stacks, stack, crates)
    end)
  end

  @doc """
  Parse and runs the procedure input on the initial stacks state
  """
  def operate(stacks, procedure_input) do
    procedure_input
    |> String.split("\n")
    |> Enum.map(&String.split(&1, " "))
    |> Enum.map(fn ["move", n, "from", from_stack, "to", to_stack] ->
      [n: String.to_integer(n), from: from_stack, to: to_stack]
    end)
    |> Enum.reduce(stacks, fn [n: n, from: from, to: to], stacks ->
      Stacker.move_n(n, stacks, from, to)
    end)
  end

  @doc """
  Recursively move crates between the given stacks, until all crates are moved
  """
  def move_n(0, stacks, _from, _to), do: stacks
  def move_n(n, stacks, from, to), do: move_n(n - 1, move(stacks, from, to), from, to)

  @doc """
  Moves one crate at a time from one stack to the top of another.
  """
  def move(stacks, from, to) do
    [crate | remaining_crates] = stacks[from]
    new_crates = [crate | stacks[to]]

    stacks
    |> Map.put(from, remaining_crates)
    |> Map.put(to, new_crates)
  end
end

final_stacks_state =
  starting_stacks
  |> Stacker.parse()
  |> Stacker.operate(procedure)

final_stacks_state
|> Map.values()
|> Enum.map(&List.first/1)
|> Enum.join()
````
